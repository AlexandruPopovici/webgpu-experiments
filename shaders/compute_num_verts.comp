#version 450 core

#include "compute_vertex_values.comp"

layout(set = 1, binding = 0, std430) buffer ActiveVoxels {
    uint active_voxels[];
};

layout(set = 1, binding = 1, std430) buffer NumVerts {
    uint num_verts[];
};

layout(set = 1, binding = 2, std140) uniform TriTable {
    // Stride alignment rules: need to pack each into 4 ivec4's
    ivec4 tri_table[256 * 4];
};

void main(void) {
    const uint voxel_id = active_voxels[gl_GlobalInvocationID.x];
    const float values[8] = compute_vertex_values(voxel_id_to_voxel(voxel_id));
    uint case_index = 0;
    for (int i = 0; i < 8; ++i) {
        if (values[i] <= isovalue) {
            case_index |= 1 << i;
        }
    }

	uint nverts = 0;
	// The triangle table gives us the mapping from index to actual
	// triangles to return for this configuration
    const uint base_index = case_index * 4;
    const int table[16] = {
        tri_table[base_index][0], tri_table[base_index][1],
        tri_table[base_index][2], tri_table[base_index][3],
        tri_table[base_index + 1][0], tri_table[base_index + 1][1],
        tri_table[base_index + 1][2], tri_table[base_index + 1][3],
        tri_table[base_index + 2][0], tri_table[base_index + 2][1],
        tri_table[base_index + 2][2], tri_table[base_index + 2][3],
        tri_table[base_index + 3][0], tri_table[base_index + 3][1],
        tri_table[base_index + 3][2], tri_table[base_index + 3][3]
    };
	for (uint t = 0; table[t] != -1; ++t) {
		++nverts;
	}
	num_verts[gl_GlobalInvocationID.x] = nverts;
}

